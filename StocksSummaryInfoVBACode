Option Explicit

Sub StocksSummaryInfo():

    'Declare variables
    
    Dim ws As Worksheet
    
    Dim r As Long
    Dim c As Long
    Dim LRow As Long
    Dim TotalStockVolumeCounter As Double
    Dim SummaryTableRow As Long
    Dim Ticker As String
    Dim BeginningOpeningPrice As Double
    Dim EndingClosingPrice As Double
    Dim YearlyChange As Double
    Dim PercentChange As Double
    
    'Loop through each worksheet in the workbook
    For Each ws In ActiveWorkbook.Worksheets
    
        'Create column labels for first summary table
        ws.Range("I1").Value = "Ticker"
        ws.Range("J1").Value = "Yearly Change"
        ws.Range("K1").Value = "Percent Change"
        ws.Range("L1").Value = "Total Stock Volume"
        
        'Find last row value
        LRow = ws.Cells(Rows.Count, 1).End(xlUp).Row
        
        'Set TotalStockVolumeCounter to 0
        TotalStockVolumeCounter = 0
        
        'Set SummaryTableRow to 2
        SummaryTableRow = 2
    
        'Set BeginningOpeningPrice to first value
        BeginningOpeningPrice = ws.Cells(2, 3).Value
        
        'Create row labels for second summary table
        ws.Range("O2").Value = "Greatest % Increase"
        ws.Range("O3").Value = "Greatest % Decrease"
        ws.Range("O4").Value = "Greatest Total Volume"
        
        'Create column lables for second summary table
        ws.Range("P1").Value = "Ticker"
        ws.Range("Q1").Value = "Value"
        
        'Set initial values for second summary table (this way we have something to compare to for the for loop to work)
        ws.Range("P2").Value = "-"
        ws.Range("P3").Value = "-"
        ws.Range("P4").Value = "-"
        ws.Range("Q2").Value = 0
        ws.Range("Q3").Value = 0
        ws.Range("Q4").Value = 0
        
        'Set up number formats for Summary Table 2
        ws.Range("Q2").NumberFormat = "0.00%"
        ws.Range("Q3").NumberFormat = "0.00%"
        
        'For loop to go through each row
        For r = 2 To LRow
        
            'If statement to check if current ticker (transaction) is different from next one
            If ws.Cells(r, 1).Value <> ws.Cells(r + 1, 1).Value Then
            
                'Set Ticker name
                Ticker = ws.Cells(r, 1).Value
                
                'Print new Ticker in Summary Table 1
                ws.Cells(SummaryTableRow, 9).Value = Ticker
                
                'Add <vol> to TotalStockVolumeCounter
                TotalStockVolumeCounter = TotalStockVolumeCounter + ws.Cells(r, 7).Value
            
                'Print TotalStockVolumeCounter in Summary Table 1
                ws.Cells(SummaryTableRow, 12).Value = TotalStockVolumeCounter
                
                'If statement to compare current TotalStockVolumeCounter and current Greatest Total Volume
                If ws.Cells(SummaryTableRow, 12).Value > ws.Range("Q4") Then
                    
                    'Print Ticker in Greatest Total Volume of Summary Table 2
                    ws.Range("P4") = Ticker
                    
                    'Print TotalStockVolumeCounter in Summary Table 2
                    ws.Range("Q4") = TotalStockVolumeCounter
                    
                End If
                
                'Reset TotalStockVolumeCounter to 0
                TotalStockVolumeCounter = 0
                
                'Set EndingClosingPrice value
                EndingClosingPrice = ws.Cells(r, 6).Value
                
                'Subtract BeginningOpeningPrice from EndingClosingPrice to get YearlyChange
                YearlyChange = EndingClosingPrice - BeginningOpeningPrice
                
                'Print YearlyChange in Summary Table 1
                ws.Cells(SummaryTableRow, 10).Value = YearlyChange
                
                'If statement to address divide by zero scenarios
                If BeginningOpeningPrice = 0 Then
                    
                    'Why are we dividing by zero???
                    ws.Cells(SummaryTableRow, 11).Value = "Not a Number"
                    
                'Most situations don't divide by zero and hence execute the else
                Else
                    
                    'Divide YearlyChange by BeginningOpeningPrice to get PercentChange
                    PercentChange = YearlyChange / BeginningOpeningPrice
                    
                    'Print PercentChange in Summary Table 1
                    ws.Cells(SummaryTableRow, 11).Value = PercentChange
                    
                    'Change number format to percentage
                    ws.Cells(SummaryTableRow, 11).NumberFormat = "0.00%"
                    
                    'If statement to compare current PercentChange and current Greatest % Increase
                    If PercentChange > ws.Range("Q2").Value Then
                        
                        'Print Ticker in Greatest % Increase of Summary Table 2
                        ws.Range("P2").Value = Ticker
                        
                        'Print PercentChange in Summary Table 2
                        ws.Range("Q2").Value = PercentChange
                        
                    'If statement to compare current PercentChange and current Greatest % Decrease
                    ElseIf PercentChange < ws.Range("Q3").Value Then
                        
                        'Print Ticker in Greatest % Increase of Summary Table 2
                        ws.Range("P3").Value = Ticker
                        
                        'Print PercentChange in Summary Table 2
                        ws.Range("Q3").Value = PercentChange
                        
                    End If
                    
                End If
                
                'If statement to check if YearlyChange is positive
                If ws.Cells(SummaryTableRow, 10).Value > 0 Then
                
                    'Color YearlyChange green
                    ws.Cells(SummaryTableRow, 10).Interior.ColorIndex = 4
                    
                'Else color YearlyChange red
                ElseIf ws.Cells(SummaryTableRow, 10).Value < 0 Then
                
                    ws.Cells(SummaryTableRow, 10).Interior.ColorIndex = 3
                
                End If
            
                'Set BeginningOpeningPrice to beginning opening price of the next ticker
                BeginningOpeningPrice = ws.Cells(r + 1, 3).Value
                
                'Add 1 to SummaryTableRow
                SummaryTableRow = SummaryTableRow + 1
            
            'Else condition on what to do when current ticker and next ticker are same
            Else
                
                'Add <vol> to TotalStockVolumeCounter
                TotalStockVolumeCounter = TotalStockVolumeCounter + ws.Cells(r, 7).Value
                
            End If
            
        'Next row
        Next r
    
    'Next worksheet
    Next ws

End Sub